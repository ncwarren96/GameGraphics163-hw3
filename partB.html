<!--
Using Three.js and GLSL shaders, create a “shadertoy style” scene:
	— The entirety of the scene will be rendered using a GLSL fragment shader. The only
	geometry will be the “full screen quad” that covers the entirety of the window. That is,
	your fragment shader will be responsible for rendering every pixel in your browser
	window.
	— Use 3D SDFs to generate the objects in the scene. Use constructive solid geometry
	(CSG) operators to create complex shapes. Emulate Phong lighting in the fragment shader.
	— Have at least one of the shapes morph into another shape over a short period of time
	(~10 seconds or so is fine).
Extra credit (10 pts):
	— Add textures to the SDF objects in the scene.
-->

<!DOCTYPE html>
<html lang="en">
	<head>
		<title>HW3 - Part B - SDF Scene</title>
		<meta charset="utf-8">
		<style>
			body {
				margin: 0px;
				overflow: hidden;
			}
		</style>
	</head>
<body>

<div id="container"></div>

<script src="js/three.js"></script>
<script src="./js/dat.gui.min.js"></script>


<script id="vshader" type="x-shader/x-vertex">
    precision mediump float;

    uniform float time;

    varying vec3 fPosition;

    void main() {
        fPosition = position;
        gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1.0);
    }
</script>

<script id="fshader" type="x-shader/x-fragment">
	precision mediump float;

	uniform float time;
	uniform vec2 resolution;

	const int MAX_MARCHING_STEPS = 255;
	const float MIN_DIST = 0.0;
	const float MAX_DIST = 100.0;
	const float EPSILON = 0.0001;

	// Signed distance function for a sphere centered at the origin with radius 1.0;
	float sphereSDF(vec3 p) {
	    return length(p) - 1.0;
	}

	// Signed distance function for a cube centered at origin with dimensions 1.0, 1.0, 1.0;
	float cubeSDF(vec3 p){
		vec3 d = abs(p) - vec3(1.0, 1.0, 1.0);
  		return min(max(d.x,max(d.y,d.z)),0.0) + length(max(d,0.0));
	}

	float triPrismSDF(vec3 p){
	    vec3 q = abs(p);
	    vec2 h = vec2(3.0, 1.0);
	    return max(q.z-h.y,max(q.x*0.866025+p.y*0.5,-p.y)-h.x*0.5);
	}

	float cylanderSDF(vec3 p){
		vec3 c = vec3(2.0, 0.1, 0.1);
		return length(p.xz-c.xy)-c.z;
	}

	float intersectSDF(float distA, float distB) {
    	return max(distA, distB);
	}

	float unionSDF(float distA, float distB) {
	    return min(distA, distB);
	}

	float differenceSDF(float distA, float distB) {
	    return max(distA, -distB);
	}

	// vec3 opTx( vec3 p, mat4 m )
	// {
	//     vec3 q = invert(m)*p;
	//     return primitive(q);
	// }

	float sceneSDF(vec3 p) {
	    //float sphereDist = sphereSDF(p / 1.2) * 1.2;
	    float cylanderDist1 = cylanderSDF(p+vec3(0.1, 0.0, 0.0));
	    float cylanderDist2 = cylanderSDF(p+vec3(1.0, 0.0, 0.0));
	    float cyls = unionSDF(cylanderDist1, cylanderDist2);

	    float triDist = triPrismSDF(p);
	    return differenceSDF(triDist, cyls);
	}

		/**
	 * Return the shortest distance from the eyepoint to the scene surface along
	 * the marching direction. If no part of the surface is found between start and end,
	 * return end.
	 * 
	 * eye: the eye point, acting as the origin of the ray
	 * marchingDirection: the normalized direction to march in
	 * start: the starting distance away from the eye
	 * end: the max distance away from the ey to march before giving up
	 */
	float shortestDistanceToSurface(vec3 eye, vec3 marchingDirection, float start, float end) {
	    float depth = start;
	    vec3 eye2 = vec3(1.0, 0.0, 5.0);
	    for (int i = 0; i < MAX_MARCHING_STEPS; i++) {
	        float dist = sceneSDF(eye + depth * marchingDirection);
	        if (dist < EPSILON) {
				return depth;
	        }
	        depth += dist;
	        if (depth >= end) {
	            return end;
	        }
	    }
	    return end;
	}

	/**
	 * Return the normalized direction to march in from the eye point for a single pixel.
	 * 
	 * fieldOfView: vertical field of view in degrees
	 * size: resolution of the output image
	 * fragCoord: the x,y coordinate of the pixel in the output image
	 */
	vec3 rayDirection(float fieldOfView, vec2 size, vec2 fragCoord) {
	    vec2 xy = fragCoord - size / 2.0;
	    float z = size.y / tan(radians(fieldOfView) / 2.0);
	    return normalize(vec3(xy, -z));
	}

	/**
	 * Using the gradient of the SDF, estimate the normal on the surface at point p.
	 */
	vec3 estimateNormal(vec3 p) {
	    return normalize(vec3(
	        sceneSDF(vec3(p.x + EPSILON, p.y, p.z)) - sceneSDF(vec3(p.x - EPSILON, p.y, p.z)),
	        sceneSDF(vec3(p.x, p.y + EPSILON, p.z)) - sceneSDF(vec3(p.x, p.y - EPSILON, p.z)),
	        sceneSDF(vec3(p.x, p.y, p.z  + EPSILON)) - sceneSDF(vec3(p.x, p.y, p.z - EPSILON))
	    ));
	}
	vec3 phongContribForLight(vec3 k_d, vec3 k_s, float alpha, vec3 p, vec3 eye,
	                          vec3 lightPos, vec3 lightIntensity) {
	    vec3 N = estimateNormal(p);
	    vec3 L = normalize(lightPos - p);
	    vec3 V = normalize(eye - p);
	    vec3 R = normalize(reflect(-L, N));
	    
	    float dotLN = dot(L, N);
	    float dotRV = dot(R, V);
	    
	    if (dotLN < 0.0) {
	        // Light not visible from this point on the surface
	        return vec3(0.0, 0.0, 0.0);
	    } 
	    
	    if (dotRV < 0.0) {
	        // Light reflection in opposite direction as viewer, apply only diffuse
	        // component
	        return lightIntensity * (k_d * dotLN);
	    }
	    return lightIntensity * (k_d * dotLN + k_s * pow(dotRV, alpha));
	}


	vec3 phongIllumination(vec3 k_a, vec3 k_d, vec3 k_s, float alpha, vec3 p, vec3 eye) {
	    const vec3 ambientLight = 0.5 * vec3(1.0, 1.0, 1.0);
	    vec3 color = ambientLight * k_a;
	    
	    vec3 light1Pos = vec3(4.0 * sin(time),
	                          2.0,
	                          4.0 * cos(time));
	    vec3 light1Intensity = vec3(0.4, 0.4, 0.4);
	    
	    color += phongContribForLight(k_d, k_s, alpha, p, eye,
	                                  light1Pos,
	                                  light1Intensity); 
	    return color;
	}

	/**
	 * Return a transform matrix that will transform a ray from view space
	 * to world coordinates, given the eye point, the camera target, and an up vector.
	 *
	 * This assumes that the center of the camera is aligned with the negative z axis in
	 * view space when calculating the ray marching direction. See rayDirection.
	 */
	mat4 myViewMatrix(vec3 eye, vec3 center, vec3 up) {
	    // Based on gluLookAt man page
	    vec3 f = normalize(center - eye);
	    vec3 s = normalize(cross(f, up));
	    vec3 u = cross(s, f);
	    return mat4(
	        vec4(s, 0.0),
	        vec4(u, 0.0),
	        vec4(-f, 0.0),
	        vec4(0.0, 0.0, 0.0, 1)
	    );
	}

	void main(){
		vec3 viewDir = rayDirection(45.0, resolution.xy, gl_FragCoord.xy);
    	vec3 eye = vec3(5.0, 5.0, 10.0);

    	mat4 viewToWorld = myViewMatrix(eye, vec3(0.0), vec3(0.0, 1.0, 0.0));

    	vec3 worldDir = (viewToWorld * vec4(viewDir, 0.0)).xyz;

    	float dist = shortestDistanceToSurface(eye, worldDir, MIN_DIST, MAX_DIST);

	    if (dist > MAX_DIST - EPSILON) {
	        // Didn't hit anything
	        gl_FragColor = vec4(0.0, 0.0, 0.0, 0.0);
			//return;
	    }

	    // closes point on the surface to the eyepoint along teh view ray
	    vec3 p = eye + dist * worldDir;
	    
	    vec3 K_a = vec3(0.2, 0.2, 0.2); //ambient color
	    vec3 K_d = vec3(0.7, 0.2, 0.2); //diffuse color
	    vec3 K_s = vec3(1.0, 1.0, 1.0); //specular color
	    float shininess = 10.0;
	    
	    vec3 color = phongIllumination(K_a, K_d, K_s, shininess, p, eye);
		gl_FragColor = vec4(color, 1.0);
	}

</script>

<script>
	const WIDTH = window.innerWidth;
	const HEIGHT = window.innerHeight;

	var vshader = document.getElementById( 'vshader' ).textContent;
	var fshader = document.getElementById( 'fshader' ).textContent;

	// 0. Create a renderer
	var renderer = new THREE.WebGLRenderer();
	renderer.setSize(WIDTH, HEIGHT);
	document.body.appendChild(renderer.domElement)

	// 1. Create a Scene
	var scene = new THREE.Scene();

	// 1.1 Create a camera
	var camera = new THREE.OrthographicCamera(WIDTH/-2, WIDTH/2, HEIGHT/2, HEIGHT/-2, 1, 1000);
	camera.position.z = 5;

	// //2. Create meshes
	var geometry = new THREE.PlaneGeometry(WIDTH, HEIGHT);

	var uniforms = {
		time: {type: 'f', value: 0.0},
		resolution: {type: 'v2', value: new THREE.Vector2(WIDTH, HEIGHT)}
	}

	var material = new THREE.ShaderMaterial({
		uniforms: uniforms,
		vertexShader: vshader,
		fragmentShader: fshader,
	});
	var fullScreenQuad = new THREE.Mesh(geometry, material);


	scene.add(fullScreenQuad);

	
	function animate() {
		requestAnimationFrame( animate );
		fullScreenQuad.material.uniforms.time.value += 0.01;
		renderer.render( scene, camera );
	}
	animate();

</script>